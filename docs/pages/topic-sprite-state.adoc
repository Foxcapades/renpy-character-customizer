[#sprite-state]
= Sprite State

To keep sprite customization options stored in game save files, the internal
selection state for the sprites is stored in a runtime variable of type
<<sc-state>>.

This state should be created via a runtime variable in a python line, or,
preferrably, a `default` statement.

[source, python]
----
default my_sprite_state_1 = SCState()
default my_sprite_state_2 = SCState()

label start:
    $ my_sprite_1.set_state(my_sprite_state_1)
    $ my_sprite_2.set_state(my_sprite_state_2)

label after_load:
    $ my_sprite_1.set_state(my_sprite_state_1)
    $ my_sprite_2.set_state(my_sprite_state_2)
----

This state is then loaded into a target <<custom-sprite>> instance to configure
all the option selections show the correct combination of options.  Each
separate <<custom-sprite>> instance should have it's own state instance.  This
means that for every unique sprite, you will need a unique `default` declaring a
new <<sc-state>> object.  Additionally, for every separate <<custom-sprite>>
instance, that state value must be loaded at `start` and `after_load` times.

[IMPORTANT]
--
This state value *MUST* be passed to the relevant sprite instance via the
`set_state` method in both the `start` and `after_load` labels.  If this is not
done, the sprites will reset to their default state.
--

== Additional State

In addition to the sprite customization option selection values, the
<<sc-state>> object may be used to store arbitrary user-defined variables that
will then be passed through to the <<Layer Callbacks>> attached to the sprite
that uses the target <<sc-state>> object.

Example::
The following code snippets attempt to demonstrate a use case for the use of
additional user-defined state to augment the CustomizedSprite's behavior so as
to make use of a non-option variable to control how the sprite gets rendered.
+
In this particular case, we have a set of sprite "base" images that represent
different player moods.  It wouldn't make sense to have the player mood be a
customization option, but it is something that does have options we wish to
control during gameplay.
+
.`config.rpy`
[source, python]
----
init python:
    def base_lcb(**kwargs):
        return "sprite_base_{}".format(kwargs["mood"])

define my_sprite = CustomizedSprite("player", SCLayer("base", base_lcb))
----
+
.`script.rpy`
[source, python]
----
default my_sprite_state = SCState()

label start:
    $ my_sprite_state.set_state("mood", "happy")
    $ my_sprite.set_state(my_sprite_state)

    show player
----